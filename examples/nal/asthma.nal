*volume=0
//Being friend is symmetric
<<$1 --> (friend /1 $2)> ==> <$2 --> (friend /1 $1)>>.
//Stressed people tend to smoke
<<$1 --> [stressed]> ==> <$1 --> [smokes]>>. {1.0 0.99}
//A smoker befriended with someone else often makes the other person smoke as well
<(<#1 --> [smokes]> && <#1 --> (friend /1 $2)>) ==> <$2 --> [smokes]>>. {1.0 0.9}
//If someone smokes, he gets asthma
<<$1 --> [smokes]> ==> <$1 --> [asthma]>>. {1.0 0.9}
//Angelika is a friend of Jonas
<{angelika} --> (friend /1 {jonas})>.
//Angelika smokes
<{angelika} --> [smokes]>.
10
//Jonas has asthma?
<{jonas} --> [asthma]>?
//expected: Answer: <{jonas} --> [asthma]>. Truth: frequency=1.000000, confidence=0.531441
//Angelika has asthma?
<{angelika} --> [asthma]>?
//expected: Answer: <{angelika} --> [asthma]>. Truth: frequency=1.000000, confidence=0.810000
//Jonas is stressed
<{jonas} --> [stressed]>.
//Jonas has asthma?
<{jonas} --> [asthma]>?
//expected: Answer: <{jonas} --> [asthma]>. Truth: frequency=1.000000, confidence=0.801900
